#!/bin/zsh
setopt prompt_subst
autoload colors
colors

schedprompt() {
  emulate -L zsh
  zmodload -i zsh/sched

  # Remove existing event, so that multiple calls to
  # "schedprompt" work OK.  (You could put one in precmd to push
  # the timer 30 seconds into the future, for example.)
  integer i=${"${(@)zsh_scheduled_events#*:*:}"[(I)schedprompt]}
  (( i )) && sched -$i

  # Test that zle is running before calling the widget (recommended
  # to avoid error messages).
  # Otherwise it updates on entry to zle, so there's no loss.
  zle && zle reset-prompt

  # This ensures we're not too far off the start of the minute
  sched +$((60 - $(date +%S))) schedprompt
}

# root prompt
[ $UID = 0 ] && export PROMPT="$(print '%{\e[1;31m%}%m%{\e[1;34m%} %24<..<%~ %<<#%{\e[0m%} ')"
# # normal user prompt
[ $UID != 0 ] && export PROMPT="$(print '%{\e[1;32m%}%n@%m%{\e[1;34m%} %24<..<%~ %<<$%{\e[0m%} ')"

get_prompt() {
    if [[ -n $(get_git_prompt_info) ]]; then
        echo "%{$fg_bold[blue]%}[%{$fg_bold[green]%}$(get_git_prompt_info)%{$fg_bold[blue]%}][%{$fg_bold[green]%}%D{%d.%m.%y %H:%M}%{$fg_bold[blue]%}]%{$reset_color%}"
    else
        echo "%{$fg_bold[blue]%}[%{$fg_bold[green]%}%D{%d.%m.%y %H:%M}%{$fg_bold[blue]%}]%{$reset_color%}"
    fi
}
RPROMPT='$(get_prompt)'
schedprompt

case $TERM in
    xterm*|rxvt*)
        precmd () {print -Pn "\e]0;%n@%m: %~\a"}
        preexec() {
            case $1 in
            fg|bg) getjobs "${(z)1}" ;;
            *) print -Pn "\e]0;$1\a" ;;
        esac
        }
        bindkey '\e[H' beginning-of-line
        bindkey '\e[F' end-of-line
        ;;
    screen*)
        function title {
            print -nR $'\033k'$1$'\033'\\\

            print -nR $'\033]0;'$2$'\a'
        }
        function precmd {
            title zsh "$PWD"
        }

        preexec () {
            emulate -L zsh
            local -a cmd; cmd=(${(z)1})
            title $cmd[1]:t "$cmd[2,-1]"
        }
        ;;
esac

getjobs () {
    setopt localoptions noshwordsplit noksharrays
    local texts
    case $1 in
        fg|bg) shift; [[ -n $1 ]] || set -- %% ;;
        %*) ;;
    esac
    repeat $#
    do
        # This case statement emulates jobs.c:getjob()
        case $1 in
        %(|[%+])) 1=${(k)jobstates[(r)*:+:*]} ;;
        %-) 1=${(k)jobstates[(r)*:-:*]} ;;
        %<->) 1=${1#%} ;;
        %[?]*) 1=${${(Ok)jobtexts[(R)*${1#%[?]}*]}[1]} ;;
        *) 1=${${(Ok)jobtexts[(R)$1*]}[1]} ;;
        esac
        [[ -n $1 ]] && texts=($texts ${jobtexts[$1]})
        shift
    done
    (( $#texts )) && print -Pn "\e]0;${texts}\a"
    return 0
}
